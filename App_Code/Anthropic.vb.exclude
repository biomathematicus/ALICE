Imports System.Net.Http
Imports System.Text
Imports System.Threading.Tasks
Imports System.Web.Script.Serialization

Namespace Auxiliator
	<CLSCompliant(False)> Public Class Anthropic : Inherits ChatAgentBase

		Public Sub New(modelCode As String, agentName As String, apiKey As String,
					   temperature As Double, maxTokens As Integer)
			MyBase.New(modelCode, agentName, apiKey, temperature, maxTokens)
		End Sub

		Protected Overrides Async Function DoApiCallAsync(userText As String) As Task(Of String)
			Dim js As New JavaScriptSerializer()
			Dim body As New Dictionary(Of String, Object) From {
			  {"model", ModelCode},
			  {"max_tokens", MaxTokensValue},
			  {"temperature", TemperatureValue},
			  {"messages", BuildMessages(userText)}
			}
			Dim json = js.Serialize(body)

			Using req As New HttpRequestMessage(HttpMethod.Post,
					 "https://api.anthropic.com/v1/messages")
				req.Headers.Add("x-api-key", ApiKey)
				req.Headers.Add("anthropic-version", "2023-06-01")
				req.Content = New StringContent(json, Encoding.UTF8, "application/json")
				Using resp = Await Http.SendAsync(req).ConfigureAwait(False)
					resp.EnsureSuccessStatusCode()
					Return ParseReply(Await resp.Content.ReadAsStringAsync().ConfigureAwait(False))
				End Using
			End Using
		End Function

		Private Function ParseReply(raw As String) As String
			Dim js As New JavaScriptSerializer()
			Dim root = js.Deserialize(Of Dictionary(Of String, Object))(raw)
			Dim parts = DirectCast(root("content"), Object())
			Return If(parts.Length > 0,
					  CStr(DirectCast(parts(0), Dictionary(Of String, Object))("text")), "")
		End Function
	End Class
End Namespace
